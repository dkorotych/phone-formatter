import io.micronaut.gradle.MicronautRuntime
import io.micronaut.gradle.MicronautTestRuntime

plugins {
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id("io.micronaut.application") version "2.0.4"
    id("org.openrewrite.rewrite") version "5.8.0"
    id("com.github.ben-manes.versions") version "0.39.0"
}

version = "0.1"
group = "com.github.dkorotych.phone.formatter"

repositories {
    mavenCentral()
}

micronaut {
    runtime(MicronautRuntime.NETTY)
    testRuntime(MicronautTestRuntime.JUNIT_5)
    processing {
        incremental(true)
        annotations("com.github.dkorotych.phone.formatter.*")
    }
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("javax.annotation:javax.annotation-api")
    compileOnly("org.projectlombok:lombok")
    runtimeOnly("ch.qos.logback:logback-classic")
    rewrite("org.openrewrite.recipe:rewrite-micronaut:1.1.0")
    implementation("io.micronaut:micronaut-validation")

    implementation "io.projectreactor:reactor-core:3.4.9"
    implementation("com.googlecode.libphonenumber:libphonenumber:8.12.32")
    implementation("com.kcthota:emoji4j:6.0")
    implementation("io.sentry:sentry-logback:5.1.2")

    testImplementation("org.skyscreamer:jsonassert:1.5.0")
    testImplementation("org.assertj:assertj-core:3.20.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.2")
}


application {
    mainClass.set("com.github.dkorotych.phone.micronaut.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

rewrite {
    activeRecipe("org.openrewrite.java.micronaut.Micronaut2to3Migration")
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkConstraints = true
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

dependencyUpdates.gradleReleaseChannel = "current"
