plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.3.4"
    id("io.micronaut.aot") version "4.3.4"
    id("org.openrewrite.rewrite") version "6.10.1"
    id("com.github.ben-manes.versions") version "0.51.0"
    id("org.sonarqube") version "4.4.1.3373"
}

version = "0.1"
group = "com.github.dkorotych.phone.formatter"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor("io.micronaut:micronaut-graal")
    annotationProcessor "io.micronaut.validation:micronaut-validation-processor"
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation "io.micronaut.validation:micronaut-validation"
    compileOnly("io.micronaut:micronaut-http-client")
    compileOnly("io.micronaut.openapi:micronaut-openapi-annotations")
    compileOnly("org.projectlombok:lombok")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    implementation("com.googlecode.libphonenumber:libphonenumber:8.13.32")
    implementation("io.sentry:sentry-logback:7.6.0")

    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("org.skyscreamer:jsonassert:1.5.1")
    testImplementation("org.assertj:assertj-core:3.25.3")
    testImplementation "org.glassfish.jaxb:jaxb-runtime:4.0.5"
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.2")
    testImplementation "com.sun.xml.ws:jaxws-rt:4.0.2"

    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:2.8.1"))
    rewrite("org.openrewrite.recipe:rewrite-github-actions")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-static-analysis")
    rewrite("org.openrewrite.recipe:rewrite-java-security")
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-migrate-java")
    rewrite("org.openrewrite.recipe:rewrite-micronaut")
    rewrite("org.openrewrite:rewrite-gradle")

    aotPlugins platform("io.micronaut.platform:micronaut-platform:${micronautVersion}")
    aotPlugins("io.micronaut.security:micronaut-security-aot")
}


application {
    mainClass.set("com.github.dkorotych.phone.micronaut.Application")
}

java {
    sourceCompatibility = JavaLanguageVersion.of(21)
    targetCompatibility = JavaLanguageVersion.of(21)
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.github.dkorotych.phone.formatter.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = true
        convertYamlToJava = true
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        configurationProperties.put("micronaut.security.jwks.enabled", "false")
    }
}

rewrite {
    activeRecipe("com.github.dkorotych.phone.formatter.CustomRecipes")
}

dependencyUpdates {
    checkForGradleUpdate = true
    revision = "release"
    gradleReleaseChannel = "current"
    resolutionStrategy {
        componentSelection {
            all { ComponentSelection selection ->
                def candidate = selection.candidate
                if (candidate.version.contains("beta")) {
                    selection.reject("Plugin ${candidate.displayName} reject because is a beta release")
                }
                if (candidate.version.matches('.+-M\\d+$')) {
                    selection.reject("Dependency ${candidate.displayName} reject because is a milestone release")
                }
                if (candidate.version.matches('.+-RC\\d+$')) {
                    selection.reject("Dependency ${candidate.displayName} reject because is a release candidate")
                }
            }
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "dkorotych_phone-formatter"
        property "sonar.organization", "dkorotych"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
