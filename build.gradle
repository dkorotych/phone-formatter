plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.1.0"
    id("io.micronaut.aot") version "4.1.0"
    id("org.openrewrite.rewrite") version "6.3.6"
    id("com.github.ben-manes.versions") version "0.48.0"
}

version = "0.1"
group = "com.github.dkorotych.phone.formatter"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    annotationProcessor "io.micronaut.validation:micronaut-validation-processor"
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation "io.micronaut.validation:micronaut-validation"
    implementation("io.swagger.core.v3:swagger-annotations")
    compileOnly("org.projectlombok:lombok")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    implementation("com.googlecode.libphonenumber:libphonenumber:8.13.20")
    implementation("com.vdurmont:emoji-java:5.1.1") {
        exclude group: "org.json", module: "json"
    }
    runtimeOnly("org.json:json:20230618")
    implementation("io.sentry:sentry-logback:6.29.0")

    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("org.skyscreamer:jsonassert:1.5.1")
    testImplementation("org.assertj:assertj-core:3.24.2")
    testImplementation "org.glassfish.jaxb:jaxb-runtime:4.0.3"
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.0")
    testImplementation "com.sun.xml.ws:jaxws-rt:4.0.1"

    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:2.3.0"))
    rewrite("org.glassfish.jaxb:jaxb-runtime:4.0.3")
    rewrite("javax.xml.bind:jaxb-api:2.3.1")
    rewrite("org.openrewrite.recipe:rewrite-github-actions")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-static-analysis")
    rewrite("org.openrewrite.recipe:rewrite-java-security")
    rewrite("org.openrewrite.recipe:rewrite-testing-frameworks")
    rewrite("org.openrewrite.recipe:rewrite-migrate-java")
    rewrite("org.openrewrite.recipe:rewrite-micronaut")
    rewrite("org.openrewrite:rewrite-gradle")

    aotPlugins platform("io.micronaut.platform:micronaut-platform:${micronautVersion}")
    aotPlugins("io.micronaut.security:micronaut-security-aot")
}


application {
    mainClass.set("com.github.dkorotych.phone.micronaut.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.github.dkorotych.phone.formatter.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        configurationProperties.put("micronaut.security.jwks.enabled", "false")
    }
}

rewrite {
    activeRecipe("com.github.dkorotych.phone.formatter.CustomRecipes")
}

dependencyUpdates {
    checkForGradleUpdate = true
    revision = "release"
    gradleReleaseChannel = "current"
    resolutionStrategy {
        componentSelection {
            all { ComponentSelection selection ->
                def candidate = selection.candidate
                if (candidate.version.contains("beta")) {
                    selection.reject("Plugin ${candidate.displayName} reject because is a beta release")
                }
                if (candidate.version.matches('.+-M\\d+$')) {
                    selection.reject("Dependency ${candidate.displayName} reject because is a milestone release")
                }
                if (candidate.version.matches('.+-RC\\d+$')) {
                    selection.reject("Dependency ${candidate.displayName} reject because is a release candidate")
                }
            }
        }
    }
}
