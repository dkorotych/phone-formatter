import io.micronaut.gradle.MicronautRuntime
import io.micronaut.gradle.MicronautTestRuntime

plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.2.1"
    id("org.openrewrite.rewrite") version "5.16.2"
    id("com.github.ben-manes.versions") version "0.42.0"
}

version = "0.1"
group = "com.github.dkorotych.phone.formatter"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("jakarta.annotation:jakarta.annotation-api")
    compileOnly("org.projectlombok:lombok")
    runtimeOnly("ch.qos.logback:logback-classic")
    rewrite("org.openrewrite.recipe:rewrite-github-actions:1.1.0")
    rewrite("org.openrewrite.recipe:rewrite-logging-frameworks:1.2.0")
    rewrite("org.openrewrite.recipe:rewrite-migrate-java:1.1.0")
    rewrite("org.openrewrite.recipe:rewrite-micronaut:1.6.0")
    implementation("io.micronaut:micronaut-validation")

    implementation "io.projectreactor:reactor-core:3.4.15"
    implementation("com.googlecode.libphonenumber:libphonenumber:8.12.43")
    implementation("com.kcthota:emoji4j:6.0")
    implementation("io.sentry:sentry-logback:5.6.1")

    testImplementation("org.skyscreamer:jsonassert:1.5.0")
    testImplementation("org.assertj:assertj-core:3.22.0")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
}


application {
    mainClass.set("com.github.dkorotych.phone.micronaut.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime(MicronautRuntime.NETTY)
    testRuntime(MicronautTestRuntime.JUNIT_5)
    processing {
        incremental(true)
        annotations("com.github.dkorotych.phone.formatter.*")
    }
}


rewrite {
    activeRecipe("com.github.dkorotych.phone.formatter.CustomRecipes")
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    checkConstraints = true
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

dependencyUpdates.gradleReleaseChannel = "current"
